
Функция БольшеИлиРавно(Версия1, Версия2, Схема) Экспорт
	Возврат НЕ Больше(Версия2, Версия1, Схема); 
КонецФункции

Функция Больше(Версия1, Версия2, Схема) Экспорт
	Если НЕ ЗначениеЗаполнено(Схема) Тогда Возврат Версия1 > Версия2 КонецЕсли;
	
	Версия1ПоСхеме = РазобратьПоСхеме(Версия1, Схема);
	Версия2ПоСхеме = РазобратьПоСхеме(Версия2, Схема);
	Версия1Соответствует = ТипЗнч(Версия1ПоСхеме) = Тип("Структура");
	Версия2Соответствует = ТипЗнч(Версия2ПоСхеме) = Тип("Структура");
	Если Версия1Соответствует И Версия2Соответствует Тогда
		Если Схема = "SemVer" Тогда
			Возврат Семвер_ВерсияБольше(Версия1ПоСхеме, Версия2ПоСхеме);
		Иначе
			ВызватьИсключение "Не поддерживаемый идентификатор схемы: " + Схема;
		КонецЕсли;
	ИначеЕсли НЕ Версия1Соответствует И НЕ Версия2Соответствует Тогда
		Возврат Версия1 > Версия2;
	Иначе
		//Версия, соответствующая схеме, считается более поздней (если в менеджере пакетов новая схема версий, пользователи должны обновляться).
		Возврат Версия1Соответствует;
	КонецЕсли;
		
КонецФункции

Функция РазобратьПоСхеме(Версия, Схема) 
	Если Схема = "SemVer" Тогда
		Возврат СемВер_Разобрать(Версия);
	Иначе
		ВызватьИсключение "Не поддерживаемый идентификатор схемы: " + Схема;
	КонецЕсли;
КонецФункции

Функция СоответствуетСхеме(Версия, Схема)
	Если Схема = "" Тогда
		Возврат Истина;	
	Иначе
		Возврат ТипЗнч(РазобратьПоСхеме(Версия, Схема)) = Тип("Структура"); 
	КонецЕсли;
КонецФункции 

#Область SemVer

// Разбирает строковое представление Семантической Версии (SemVer) на структуру данных. Если версия не соответствует спецификации, возвращает Ложь.
// Подробнее см. semver.org
// Параметры:
//  Версия	 - Строка - строковое представление Семантической Версии.
// 
// Возвращаемое значение:
//   Структура, Булево - если версия соответствует спецификации, возвращает структуру с ключами;
//    * Основная - Строка - основная (MAJOR) версия.
//    * Малая - Строка - малая (MINOR) версия.
//    * Патч - Строка - версия патча (PATCH).
//	  * ПредРелиз - Массив - содержит строковые идентификаторы предрелиза (pre-release). Может быть пустым
//	  * Сборка - Массив - содержит строковые идентификаторы сборки (build). Может быть пустым
//	В противном случае возвращает Ложь.
//
Функция СемВер_Разобрать(Версия)
	Семвер = Новый Структура;
	Семвер.Вставить("ПредРелиз", Новый Массив);
	Семвер.Вставить("Сборка", Новый Массив);
	
	ПерваяТочка = СтрНайти(Версия, ".");
	ВтораяТочка = СтрНайти(Версия, ".", , , 2);
	Если ВтораяТочка = 0 Тогда Возврат Ложь; КонецЕсли;
	Основная = Лев(Версия, ПерваяТочка - 1);
	Малая = Сред(Версия, ПерваяТочка + 1, ВтораяТочка - ПерваяТочка - 1);
	Если НЕ А1Э_Строки.ТолькоЦифры(Основная) Тогда Возврат Ложь; КонецЕсли;
	Если НЕ А1Э_Строки.ТолькоЦифры(Малая) Тогда Возврат Ложь; КонецЕсли;
	Семвер.Вставить("Основная", Число(Основная));
	Семвер.Вставить("Малая", Число(Малая));
	ОстатокВерсии = Сред(Версия, ВтораяТочка + 1);
	ДлинаОстатка = СтрДлина(ОстатокВерсии);
	Сч = 1;
	Пока ДлинаОстатка >= Сч И ЭтоЦифра(Сред(ОстатокВерсии, Сч, 1)) Цикл
		Сч = Сч + 1;
	КонецЦикла;
	Если Сч = 1 Тогда Возврат Ложь; КонецЕсли; //Версия патча не обнаружена.
	Семвер.Вставить("Патч", Число(Лев(ОстатокВерсии, Сч - 1)));
	ОстатокВерсии = Сред(ОстатокВерсии, Сч);
	Если СтрДлина(ОстатокВерсии) = 0 Тогда Возврат Семвер; КонецЕсли;
	Если Лев(ОстатокВерсии, 1) = "-" Тогда
		Плюс = СтрНайти(ОстатокВерсии, "+");
		Если Плюс = 0 Тогда
			ПредРелизСтрока = Сред(ОстатокВерсии, 2);
			ОстатокВерсии = "";
		Иначе
			ПредРелизСтрока = Сред(ОстатокВерсии, 2, Плюс - 2);
			ОстатокВерсии = Сред(ОстатокВерсии, Плюс);
		КонецЕсли;
		СемВер.Вставить("ПредРелиз", СтрРазделить(ПредРелизСтрока, "."));
	КонецЕсли;
	Если СтрДлина(ОстатокВерсии) = 0 Тогда Возврат Семвер; КонецЕсли;
	Если Лев(ОстатокВерсии, 1) = "+" Тогда
		СборкаСтрока = Сред(ОстатокВерсии, 2);
		ОстатокВерсии = "";
		СемВер.Вставить("Сборка", СтрРазделить(СборкаСтрока, "."));
	КонецЕсли;
	Если СтрДлина(ОстатокВерсии) = 0 Тогда Возврат Семвер; КонецЕсли;
	Возврат Ложь;
КонецФункции

// Сравнивает Семантические Версии. Согласно спецификации, версии сравниваются по Основной, потом по Главной, потом по Патчу, потом по предрелизу. Подробнее см. semver.org.
//
// Параметры:
//  Версия1	 - Структура - структура, полученная при разборке строкового представления версии
//  Версия2	 - Структура - см. выше.
// 
// Возвращаемое значение:
//   - 
//
Функция Семвер_ВерсияБольше(Версия1, Версия2) 
	Если Версия1.Основная <> Версия2.Основная Тогда Возврат Версия1.Основная > Версия2.Основная КонецЕсли;
	Если Версия1.Малая <> Версия2.Малая Тогда Возврат Версия1.Малая > Версия2.Малая КонецЕсли;
	Если Версия1.Патч <> Версия2.Патч Тогда Возврат Версия1.Патч > Версия2.Патч КонецЕсли;
	//Последовательно сравниваем предрелизные идентификаторы.
	Сч = -1;
	Пока Сч < Версия1.ПредРелиз.Количество() - 1 И Сч < Версия1.ПредРелиз.Количество() - 1 Цикл
		Сч = Сч + 1;
		ПредРелиз1 = Версия1.ПредРелиз[Сч];
		ПредРелиз2 = Версия2.ПредРелиз[Сч];
		ПредРелиз1Числовой = А1Э_Строки.ТолькоЦифры(ПредРелиз1);
		ПредРелиз2Числовой = А1Э_Строки.ТолькоЦифры(ПредРелиз2);
		Если ПредРелиз1Числовой И ПредРелиз2Числовой Тогда
			ПредРелиз1Число = Число(ПредРелиз1);
			ПредРелиз2Число = Число(ПредРелиз2);
			Если ПредРелиз1Число = ПредРелиз2Число Тогда
				Продолжить;
			КонецЕсли;
			Возврат ПредРелиз1Число > ПредРелиз2Число;
		ИначеЕсли НЕ ПредРелиз1Числовой И НЕ ПредРелиз2Числовой Тогда
			Если ПредРелиз1 = ПредРелиз2 Тогда
				Продолжить;
			КонецЕсли;
			Возврат ПредРелиз1 > ПредРелиз2;
		Иначе
			Возврат НЕ ПредРелиз1Числовой;
		КонецЕсли;
	КонецЦикла;
	Возврат Версия1.ПредРелиз.Количество() > Версия2.ПредРелиз.Количество();
	//В соответствии со спецификацией сборка игнорируется при сравнении.
КонецФункции

Функция ЭтоЦифра(Символ)
	Возврат СтрНайти("1234567890", Символ) <> 0;	
КонецФункции 

#КонецОбласти
